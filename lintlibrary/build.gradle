buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'io.github.gbfansheng'
version = VERSION_LINT_LIBRARY

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
    }
}

dependencies {
    lintPublish project(':checks')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'io.github.gbfansheng'
                artifactId = 'lint'
                version = version

                artifact sourcesJar

                pom {
                    name = 'lint'
                    description = 'Android lint library'
                    url = 'https://github.com/gbfansheng/AndroidLint'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gbfansheng'
                            name = 'gbfansheng'
                        }
                    }
                    scm {
                        url = 'https://github.com/gbfansheng/AndroidLint'
                        connection = 'scm:git:https://github.com/gbfansheng/AndroidLint.git'
                        developerConnection = 'scm:git:git@github.com:gbfansheng/AndroidLint.git'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "jreleaserStaging"
                url = layout.buildDirectory.dir("jreleaser/staging")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey") ?: System.getenv("JRELEASER_GPG_SECRET_KEY")
    def signingPassword = findProperty("signingPassword") ?: System.getenv("JRELEASER_GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}
